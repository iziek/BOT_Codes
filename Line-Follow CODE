// Motor pins
const int motorLeftForward = A0;
const int motorLeftBackward = A1;
const int motorRightForward = A2;
const int motorRightBackward = A3;
const int pwmLeft = 9;    // ENA pin on L298N for left motor speed
const int pwmRight = 10;  // ENB pin on L298N for right motor speed

// IR sensor pins (rightmost to leftmost)
const int sensorPins[5] = {2, 3, 4, 5, 6};

const int baseSpeed = 50;    // Base PWM speed (0-255)
const int maxSpeed = 155;

float Kp = 35.0;
float Ki = 0.0;
float Kd = 50.0;

int lastError = 0;
int integral = 0;

void setup() {
  pinMode(motorLeftForward, OUTPUT);
  pinMode(motorLeftBackward, OUTPUT);
  pinMode(motorRightForward, OUTPUT);
  pinMode(motorRightBackward, OUTPUT);
  for (int i = 0; i < 5; i++) pinMode(sensorPins[i], INPUT);
  delay(2000);
}

void stopMotors() {
  analogWrite(motorLeftForward, 0);
  analogWrite(motorLeftBackward, 0);
  analogWrite(motorRightForward, 0);
  analogWrite(motorRightBackward, 0);
  integral = 0;
  lastError = 0;
}

int readLineSensors() {
  int weights[5] = {-3, -1, 0, 1, 3};
  int error = 0;
  int count = 0;
  for (int i = 0; i < 5; i++) {
    bool onLine = (digitalRead(sensorPins[i]) == LOW);
    if (onLine) {
      error += weights[i];
      count++;
    }
  }
  if (count == 0) return lastError;  // If no line detected, continue using last error
  return error / count;
}

void setLeftMotor(int speed) {
  speed = constrain(speed, -maxSpeed, maxSpeed);
  if (speed >= 0) {
    analogWrite(motorLeftForward, speed);
    analogWrite(motorLeftBackward, 0);
  } else {
    analogWrite(motorLeftForward, 0);
    analogWrite(motorLeftBackward, -speed);
  }
}

void setRightMotor(int speed) {
  speed = constrain(speed, -maxSpeed, maxSpeed);
  if (speed >= 0) {
    analogWrite(motorRightForward, speed);
    analogWrite(motorRightBackward, 0);
  } else {
    analogWrite(motorRightForward, 0);
    analogWrite(motorRightBackward, -speed);
  }
}

void loop() {
  int error = readLineSensors();

  integral += error;
  int derivative = error - lastError;
  lastError = error;

  float correction = Kp * error + Ki * integral + Kd * derivative;

  int leftSpeed = baseSpeed - correction;
  int rightSpeed = baseSpeed + correction;

  setLeftMotor(leftSpeed);
  setRightMotor(rightSpeed);
}
